uniform sampler2D Texture;
uniform lowp float BlendIntensity;
uniform sampler2D MaskTexture;
varying mediump vec2 vTexCoord;

uniform mediump float kzTime;

#define round(x, f) (floor((x)/(f) + 0.5) * (f))

precision lowp float;
float random(float p)
{
    return fract(52.043*sin(p*205.429));
}
float random2(float p)
{
    return random(p)*2.0-1.0;
}

float smin( float a, float b, float k )
{
    float h = clamp( 0.5+0.5*(b-a)/k, 0.0, 1.0 );
    return mix( b, a, h ) - k*h*(1.0-h);
}

float sign2(float x)
{
    return x<0.0 ? -1.0 : 1.0;
}


vec3 stars(vec2 uv)
{
    vec2 r = round(uv, 0.1);
    
    float rand = r.x*2.32 + r.y;
    
    uv -= r;
    uv.x += 0.05 * random2(rand);
    uv.y += 0.05 * random2(rand+0.541);
    
    float blink = random(rand+0.5) < 0.1 ? 0.8 + 0.2 * sin(35.0*kzTime+random(rand+1.5)) : 0.0;
    float dark = random(rand+52.0) < 0.5 ? 1.0 : 0.3;
    
    return vec3(dark * max(0.0, 0.8 + blink -500.0 * length(uv)));
}

vec3 meteor(vec2 uv, float gtime, float delay)
{
    float seed = round(gtime, delay);
    
    float startTime = (delay - 1.5) * random(seed);
    float time = max(0.0, min(1.0, gtime-seed - startTime));
    
    vec2 start = vec2(
        random2(seed),
        0.7 + 0.3 * random(seed+0.1)
    );
    
    vec2 end = start * 0.5;
    
    uv = uv - mix(start, end, time);
    
    end = normalize(end - start);
    uv = uv * mat2(end.x, end.y, -end.y, end.x);
    uv.x *= 0.1;
    
    float alpha = 16.0 * pow(time, 2.0) * pow(time - 1.0, 2.0);
    return vec3(max(0.0, alpha - 500.0 * length(uv)));
}

vec3 meteorstorm(vec2 uv)
{
    return
        meteor(uv, kzTime, 9.5837) +
        meteor(uv, kzTime + 15.3, 15.459) +
        meteor(uv, kzTime + 125.0, 31.2);
}

void main()
{
    vec2 guv = (vTexCoord.xy - vec2(0.5, 0.0));
    float gv = 20.0;
    float gz = 20.0;
    
   
    
    vec3 col =
        stars(guv) +
        meteorstorm(guv)
       ;
    
    gl_FragColor.rgba = vec4(sqrt(col), 1.0);
}